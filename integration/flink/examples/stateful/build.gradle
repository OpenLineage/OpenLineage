plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'com.diffplug.spotless' version '6.22.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "pmd"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.9.1"
}

pmd {
    consoleOutput = true
    toolVersion = "6.46.0"
    rulesMinimumPriority = 5
    ruleSetFiles = rootProject.files("pmd-openlineage.xml")
    ruleSets = []
    ignoreFailures = true
}

pmdMain {
    dependsOn shadowJar
    excludes = [
        '**/InputEvent.java',
        '**/OutputEvent.java',
        '**/Counter.java',
    ]
    reports {
        html.required = true
    }
}

group 'io.openlineage.flink'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

jar.enabled = false

shadowJar {
    zip64 true
    manifest {
        attributes 'Main-Class': 'io.openlineage.flink.FlinkStatefulApplication'
    }
}

ext {
    flinkVersion = project.getProperty('flink.version')
    flinkVersionShort = flinkVersion.substring(0,4)
    hadoopVersion = '3.3.1'
    icebergVersion = "1.3.0"

    versionsMap = [
            "1.15": ["cassandra": "1.15.4"],
            "1.16": ["cassandra": "3.0.0-1.16"],
            "1.17": ["cassandra": "3.1.0-1.17"]
    ]

    versions = versionsMap[flinkVersionShort]
}

dependencies {
    implementation project(':')
    configurations.all {
        exclude module: 'slf4j-log4j12'
        exclude module: 'log4j'
    }

    testRuntimeOnly "org.junit.jupiter:junit-jupiter:5.10.1"
    testCompileOnly 'org.codehaus.groovy:groovy-all:3.0.19'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    implementation 'org.awaitility:awaitility:4.2.0'

    implementation("io.openlineage:openlineage-java:$project.version")
    compileOnly "org.apache.flink:flink-java:$flinkVersion"
    compileOnly "org.apache.flink:flink-streaming-scala_2.12:$flinkVersion"
    compileOnly "org.apache.flink:flink-streaming-java:$flinkVersion"
    compileOnly "org.apache.flink:flink-runtime-web:$flinkVersion"
    compileOnly "org.apache.flink:flink-core:$flinkVersion"
    implementation "org.apache.flink:flink-runtime:$flinkVersion"
    implementation "org.apache.flink:flink-connector-kafka:$flinkVersion"
    implementation "org.apache.flink:flink-connector-cassandra_2.12:${versions.cassandra}"
    implementation "org.apache.flink:flink-avro-confluent-registry:$flinkVersion"
    implementation "org.apache.flink:flink-avro:$flinkVersion"
    compileOnly "org.apache.flink:flink-table:$flinkVersion"
    compileOnly "org.apache.flink:flink-table-api-java:$flinkVersion"
    compileOnly "org.apache.flink:flink-table-api-java-bridge:$flinkVersion"
    compileOnly "org.apache.flink:flink-table-common:$flinkVersion"
    compileOnly "org.apache.flink:flink-table-runtime:$flinkVersion"
    compileOnly "org.apache.flink:flink-table-planner_2.12:$flinkVersion"

    implementation "com.typesafe:config:1.4.3"
    implementation "org.apache.avro:avro:1.11.3"
    implementation 'io.confluent:kafka-avro-serializer:7.5.2'
    implementation 'io.confluent:kafka-schema-registry-client:7.5.2'
    implementation 'com.github.davidmc24.gradle.plugin:gradle-avro-plugin:1.9.1'
    implementation "org.apache.hadoop:hadoop-hdfs:$hadoopVersion"
    implementation "org.apache.hadoop:hadoop-common:$hadoopVersion"
    implementation("org.apache.hadoop:hadoop-minicluster:$hadoopVersion") {
        exclude group: 'org.apache.avro', module: 'avro'
    }
    implementation "org.apache.avro:avro"

    implementation "org.apache.iceberg:iceberg-flink-runtime-$flinkVersionShort:$icebergVersion"
}

assemble {
    dependsOn shadowJar
}

avro {
    fieldVisibility = 'PUBLIC'
}

test {
    useJUnitPlatform()
}
