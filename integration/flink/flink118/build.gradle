/*
/* Copyright 2018-2024 contributors to the OpenLineage project
/* SPDX-License-Identifier: Apache-2.0
*/

plugins {
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    id 'com.adarshr.test-logger'
    id 'com.diffplug.spotless'
    id 'io.franzbecker.gradle-lombok'
    id 'io.openlineage.common-config'
}

group 'io.openlineage.flink'

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def commonTestConfiguration = {
    classpath =  project.sourceSets.test.runtimeClasspath
}

test {
    configure commonTestConfiguration
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
}

ext {
    flinkVersion = project.getProperty('flink.flink118.version')
    assertjVersion = '3.26.3'
    junit5Version = '5.11.2'
    mockitoVersion = '5.2.0'
    jdbcVersion = '3.2.0-1.19'
}

dependencies {
    implementation(project(path: ":shared"))
    api "io.openlineage:openlineage-java:${project.version}"
    api "io.openlineage:openlineage-sql-java:${project.version}"

    testImplementation "org.apache.flink:flink-java:$flinkVersion"
    testImplementation "org.apache.flink:flink-streaming-java:$flinkVersion"
    testImplementation "org.apache.flink:flink-runtime-web:$flinkVersion"
    testImplementation "org.apache.flink:flink-connector-base:$flinkVersion"
    testImplementation "org.apache.flink:flink-connector-jdbc:${jdbcVersion}"
    testImplementation "org.apache.flink:flink-table-common:$flinkVersion"
    testImplementation platform('org.junit:junit-bom:5.11.2')
    testImplementation 'org.hamcrest:hamcrest-library:3.0'
    testImplementation 'org.mock-server:mockserver-client-java:5.15.0'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.2'
    testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.24.1'

    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5Version}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

task createVersionProperties(dependsOn: processResources) {
    doLast {
        File dir = new File("$buildDir/resources/main/io/openlineage/flink/client/")
        dir.mkdirs()
        new File("$buildDir/resources/main/io/openlineage/flink/client/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionProperties
}