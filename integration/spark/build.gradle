/*
 * SPDX-License-Identifier: Apache-2.0.
 */

import org.apache.tools.ant.filters.ReplaceTokens
import groovy.io.FileType

import java.nio.file.Files

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "com.adarshr.test-logger" version "2.1.1"
    id 'com.diffplug.spotless' version '5.12.1'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://datakin.jfrog.io/artifactory/maven-public-libs-snapshot'
    }
}

configurations {
    spark3Test.extendsFrom testImplementation
    spark2Test.extendsFrom testImplementation
    spark3.extendsFrom implementation
    lombok
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

archivesBaseName='openlineage-spark'

ext {
    assertjVersion = '3.20.2'
    bigqueryVersion = '0.21.1'
    junit5Version = '5.7.2'
    sparkVersion = '2.4.8'
    jacksonVersion = '2.6.7'
    jacksonVersionSpark3 = '2.10.0'
    jacksonModuleScalaVersion = '2.6.7.1'
    jacksonDatatypeVersion = '2.6.7'
    jacksonDatabindVersion = '2.6.7.3'
    spark3Version = '3.1.2'
    postgresqlVersion = '42.2.19'
    lombokVersion = '1.18.20'
    mockitoVersion = '3.11.2'
    testcontainersVersion = '1.15.3'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
    isSpark3 = project.getProperty('spark.version').startsWith('3')
    scalaVersion = isSpark3 ? '2.12' : '2.11'
    kafkaPackage = { scalaVersion, sparkVersion->
        return "org.apache.spark:spark-sql-kafka-0-10_${scalaVersion}:${sparkVersion}"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api("io.openlineage:openlineage-java:${project.version}")

    implementation 'org.javassist:javassist:3.27.0-GA'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.0.3'
    implementation ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeVersion}"){
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
    }
    compileOnly "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    compileOnly "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compileOnly "com.fasterxml.jackson.module:jackson-module-scala_2.11:${jacksonModuleScalaVersion}"
    compileOnly "org.apache.spark:spark-core_2.11:${sparkVersion}"
    compileOnly "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    compileOnly ('com.google.cloud.spark:spark-bigquery_2.11:0.21.1') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.module'
    }
    compileOnly "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    compileOnly "org.apache.spark:spark-sql-kafka-0-10_2.11:${sparkVersion}"
    compileOnly "com.databricks:dbutils-api_${scalaVersion}:0.0.5"

    testImplementation platform('org.junit:junit-bom:5.7.1')
    testImplementation "org.postgresql:postgresql:${postgresqlVersion}"
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation('org.xerial:sqlite-jdbc:3.34.0')
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:mockserver:${testcontainersVersion}"
    testImplementation "org.testcontainers:kafka:${testcontainersVersion}"
    testImplementation "org.apache.kafka:kafka-clients:2.6.0"
    testImplementation('org.mock-server:mockserver-client-java:5.12.0') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
        exclude group: 'com.fasterxml.jackson.dataformat'
    }
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.1.1'
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5Version}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.apache.spark:spark-core_2.11:${sparkVersion}"
    testCompileOnly "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    testCompileOnly "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    testCompileOnly "com.google.cloud.spark:spark-bigquery-with-dependencies_2.11:${bigqueryVersion}"

    spark3Test "com.fasterxml.jackson.core:jackson-databind:${jacksonVersionSpark3}"
    spark3Test "com.fasterxml.jackson.core:jackson-core:${jacksonVersionSpark3}"
    spark3Test "com.fasterxml.jackson.module:jackson-module-scala_2.12:${jacksonVersionSpark3}"
    spark3Test "com.google.cloud.spark:spark-bigquery-with-dependencies_2.12:${bigqueryVersion}"
    spark3Test "org.apache.spark:spark-core_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-sql_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-hive_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-catalyst_2.12:${spark3Version}"
    spark3Test kafkaPackage('2.12', spark3Version)
    spark3Test "org.apache.iceberg:iceberg-spark3-runtime:0.12.1"
    spark3Test "io.delta:delta-core_2.12:1.0.0"

    spark2Test "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    spark2Test "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    spark2Test "com.fasterxml.jackson.module:jackson-module-scala_2.11:${jacksonVersion}"
    spark2Test "com.google.cloud.spark:spark-bigquery-with-dependencies_2.11:${bigqueryVersion}"
    spark2Test "org.apache.spark:spark-core_2.11:${sparkVersion}"
    spark2Test "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    spark2Test "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    spark2Test kafkaPackage('2.11', sparkVersion)

    spark3 "org.projectlombok:lombok:${lombokVersion}"
    spark3 "com.fasterxml.jackson.module:jackson-module-scala_2.12:${jacksonVersionSpark3}"
    spark3 "com.google.cloud.spark:spark-bigquery_2.12:${bigqueryVersion}"
    spark3 "com.google.cloud.spark:spark-bigquery-with-dependencies_2.12:${bigqueryVersion}"
    spark3 "org.apache.spark:spark-core_2.12:${spark3Version}"
    spark3 "org.apache.spark:spark-sql_2.12:${spark3Version}"
    spark3 "org.apache.spark:spark-hive_2.12:${spark3Version}"
    spark3 kafkaPackage('2.12', spark3Version)
    spark3 "org.apache.iceberg:iceberg-spark3-runtime:0.12.1"
    spark3 "io.delta:delta-core_2.12:1.0.0"

    lombok  "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

sourceSets {
    main.java.srcDirs = ["src/main/common/java"]
    test.java.srcDirs = ["src/test/common/java"]

    spark2 {
        java {
            srcDirs = ["src/main/spark2/java"]
            compileClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/main/"))
        }
    }
    spark2Test {
        java {
            srcDirs = ["src/test/spark2/java"]
            compileClasspath = sourceSets.test.compileClasspath + sourceSets.test.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/test/"))
        }
    }

    spark3 {
        java {
            srcDirs = ["src/main/spark3/java"]
            compileClasspath = configurations.spark3 + sourceSets.main.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/main/"))
        }
    }

    spark3Test {
        java {
            srcDirs = ["src/test/common/java", "src/test/spark3/java"]
            compileClasspath = configurations.spark3Test + sourceSets.main.output + sourceSets.test.output + sourceSets.spark3.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/test/"))
        }
    }
}

compileJava.finalizedBy compileSpark2Java, compileSpark3Java
compileTestJava.finalizedBy compileSpark2TestJava, compileSpark3TestJava

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

tasks.register('copyDependencies', Copy) {
    // delete the dependencies directory so we don't accidentally mix Spark 2 and Spark 3 dependencies
    delete layout.buildDirectory.dir("dependencies")
    def config = isSpark3 ? configurations.spark3Test : configurations.spark2Test
    from config.getFiles()
    include "*.jar"
    into layout.buildDirectory.dir("dependencies")
}

def commonTestConfiguration = {
    forkEvery 1
    maxParallelForks 5
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    systemProperties = [
            'junit.platform.output.capture.stdout': 'true',
            'junit.platform.output.capture.stderr': 'true',
            'spark.version'                       : project.getProperty('spark.version'),
            'openlineage.spark.jar': "${archivesBaseName}-${project.version}.jar",
            'kafka.package.version': kafkaPackage(scalaVersion, project.getProperty('spark.version')),
            'mockserver.logLevel': 'ERROR'
    ]
    classpath =  project.sourceSets.test.runtimeClasspath +
            (isSpark3 ? configurations.spark3Test : configurations.spark2Test)
}

test {
    configure commonTestConfiguration
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
    if (isSpark3) exclude 'io/openlineage/spark2/**'
    if (!isSpark3) exclude 'io/openlineage/spark3/**'
    dependsOn copyDependencies
}

task integrationTest(type: Test) {
    configure commonTestConfiguration
    useJUnitPlatform {
        includeTags "integration-test"
    }
    if (isSpark3) exclude 'io/openlineage/spark2/**'
    if (!isSpark3) exclude 'io/openlineage/spark3/**'
    dependsOn copyDependencies, shadowJar
}

integrationTest.outputs.upToDateWhen { false }

javadoc {
    options.tags = [ "apiNote" ]
}

spotless {
    def disallowWildcardImports = {
        String text = it
        def regex = ~/import .*\.\*;/
        def m = regex.matcher(text)
        if (m.find()) {
            throw new AssertionError("Wildcard imports disallowed - ${m.findAll()}")
        }
    }
    java {
        googleJavaFormat()
        removeUnusedImports()
        custom 'disallowWildcardImports', disallowWildcardImports
    }
}

def reportsDir = "${buildDir}/reports";
def coverageDir = "${reportsDir}/coverage";

jacoco {
    toolVersion = '0.8.5'
    reportsDir = file(coverageDir)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination = file(coverageDir)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.openlineage'
            artifactId = 'openlineage-spark'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'openlineage-spark'
                description = 'Java library for OpenLineage'
                url = 'https://github.com/OpenLineage/OpenLineage'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'openlineage'
                        name = 'OpenLineage Project'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/OpenLineage/OpenLineage.git'
                    developerConnection = 'scm:git:ssh://github.com:OpenLineage/OpenLineage.git'
                    url = 'https://github.com/OpenLineage/OpenLineage'
                }
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "version": project.property("version")
        ]
    }

    repositories {
        maven {
            url = isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2' :
                    'https://datakin.jfrog.io/artifactory/maven-public-libs-snapshot'
            credentials {
                username = System.getenv('RELEASE_USERNAME')
                password = System.getenv('RELEASE_PASSWORD')
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

shadowJar {
    minimize()
    classifier = ''
    // avoid conflict with any client version of that lib
    relocate 'com.github.ok2c.hc5', 'openlineage.com.github.ok2c.hc5'
    relocate 'org.apache.httpcomponents.client5', 'openlineage.org.apache.httpcomponents.client5'
    relocate 'javassist', 'openlineage.javassist'
    relocate 'org.apache.hc', 'openlineage.hc'
    relocate 'com.fasterxml.jackson.datatype.jsr310', 'openlineage.jackson.datatype.jsr310'
    relocate 'org.apache.commons.codec', 'openlineage.commons.codec'
    dependencies {
        exclude(dependency('org.slf4j::'))
    }

    manifest {
        attributes(
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version'),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    zip64 true
}

assemble {
    dependsOn shadowJar
}

task createVersionProperties(dependsOn: processResources) {
    doLast {
        File dir = new File("$buildDir/resources/main/io/openlineage/spark/agent/client/")
        dir.mkdirs();
        new File("$buildDir/resources/main/io/openlineage/spark/agent/client/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionProperties
}
