/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens
import groovy.io.FileType

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.adarshr:gradle-test-logger-plugin:2.1.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.12.1'
    }
}

plugins {
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://datakin.jfrog.io/artifactory/maven-public-libs-snapshot'
    }
}

configurations {
    spark3Test
    spark2Test
    spark3.extendsFrom implementation
    spark3Test.extendsFrom testImplementation
    lombok
}

apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.diffplug.spotless"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

archivesBaseName='openlineage-spark'

ext {
    assertjVersion = '3.20.2'
    bigqueryVersion = '0.21.1'
    junit5Version = '5.7.2'
    sparkVersion = '2.4.8'
    jacksonVersion = '2.6.7'
    jacksonVersionSpark3 = '2.10.0'
    jacksonModuleScalaVersion = '2.6.7.1'
    jacksonDatatypeVersion = '2.6.7'
    jacksonDatabindVersion = '2.6.7.3'
    spark3Version = '3.1.0'
    postgresqlVersion = '42.2.19'
    lombokVersion = '1.18.20'
    mockitoVersion = '3.11.2'
    testcontainersVersion = '1.15.3'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
    isSpark3 = project.getProperty('spark.version').startsWith('3')
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation("io.openlineage:openlineage-java:${project.version}"){
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
    }
    implementation 'org.javassist:javassist:3.27.0-GA'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.0.3'
    implementation ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeVersion}"){
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
    }
    compileOnly "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    compileOnly "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compileOnly "com.fasterxml.jackson.module:jackson-module-scala_2.11:${jacksonModuleScalaVersion}"
    compileOnly "org.apache.spark:spark-core_2.11:${sparkVersion}"
    compileOnly "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    compileOnly 'com.google.cloud.spark:spark-bigquery_2.11:0.21.1'
    compileOnly "org.apache.spark:spark-hive_2.11:${sparkVersion}"

    testImplementation platform('org.junit:junit-bom:5.7.1')
    testImplementation "org.postgresql:postgresql:${postgresqlVersion}"
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation('org.xerial:sqlite-jdbc:3.34.0')
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:mockserver:${testcontainersVersion}"
    testImplementation('org.mock-server:mockserver-client-java:5.11.2') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
    }
    testCompile "org.assertj:assertj-core:${assertjVersion}"
    testCompile "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testCompile "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.apache.spark:spark-core_2.11:${sparkVersion}"
    testCompileOnly "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    testCompileOnly "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    testCompileOnly "com.google.cloud.spark:spark-bigquery-with-dependencies_2.11:${bigqueryVersion}"

    spark3Test "com.fasterxml.jackson.core:jackson-databind:${jacksonVersionSpark3}"
    spark3Test "com.fasterxml.jackson.core:jackson-core:${jacksonVersionSpark3}"
    spark3Test "com.fasterxml.jackson.module:jackson-module-scala_2.12:${jacksonVersionSpark3}"
    spark3Test "com.google.cloud.spark:spark-bigquery-with-dependencies_2.12:${bigqueryVersion}"
    spark3Test "org.apache.spark:spark-core_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-sql_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-hive_2.12:${spark3Version}"
    spark3Test "org.apache.spark:spark-catalyst_2.12:${spark3Version}"

    spark2Test "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    spark2Test "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    spark2Test "com.fasterxml.jackson.module:jackson-module-scala_2.11:${jacksonVersion}"
    spark2Test "com.google.cloud.spark:spark-bigquery-with-dependencies_2.11:${bigqueryVersion}"
    spark2Test "org.apache.spark:spark-core_2.11:${sparkVersion}"
    spark2Test "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    spark2Test "org.apache.spark:spark-sql_2.11:${sparkVersion}"

    spark3 "org.projectlombok:lombok:${lombokVersion}"
    spark3 'io.openlineage:openlineage-java:0.0.1-SNAPSHOT'
    spark3 "com.fasterxml.jackson.module:jackson-module-scala_2.12:${jacksonVersionSpark3}"
    spark3 "com.google.cloud.spark:spark-bigquery_2.12:${bigqueryVersion}"
    spark3 "com.google.cloud.spark:spark-bigquery-with-dependencies_2.12:${bigqueryVersion}"
    spark3 "org.apache.spark:spark-core_2.12:${spark3Version}"
    spark3 "org.apache.spark:spark-sql_2.12:${spark3Version}"
    spark3 "org.apache.spark:spark-hive_2.12:${spark3Version}"

    lombok  "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

sourceSets {
    main.java.srcDirs = ["src/main/common/java", "src/main/spark2/java"]
    test.java.srcDirs = ["src/test/common/java", "src/test/spark2/java"]

    spark3{
        java {
            srcDirs = ["src/main/common/java", "src/main/spark3/java"]
            compileClasspath = configurations.spark3 + sourceSets.main.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/main/"))
        }
    }

    spark3Test {
        java {
            srcDirs = ["src/test/spark3/java"]
            compileClasspath = configurations.spark3Test + sourceSets.main.output
            annotationProcessorPath = configurations.lombok
            destinationDirectory.set(file("$buildDir/classes/java/test/"))
        }
    }
}

compileJava.finalizedBy compileSpark3Java
compileTestJava.finalizedBy compileSpark3TestJava

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def commonTestConfiguration = {
    forkEvery 1
    maxParallelForks 3
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    systemProperties = [
            'junit.platform.output.capture.stdout': 'true',
            'junit.platform.output.capture.stderr': 'true',
            'spark.version'                       : project.getProperty('spark.version'),
            'openlineage.spark.jar': "${archivesBaseName}-${project.version}.jar"
    ]
    classpath =  project.sourceSets.test.runtimeClasspath + (isSpark3 ? configurations.spark3Test : configurations.spark2Test)
}

test {
    configure commonTestConfiguration
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
    if (isSpark3) exclude 'io/openlineage/spark2/**'
    if (!isSpark3) exclude 'io/openlineage/spark3/**'
}

task integrationTest(type: Test) {
    configure commonTestConfiguration
    useJUnitPlatform {
        includeTags "integration-test"
    }
    dependsOn shadowJar
}

spotless {
    def disallowWildcardImports = {
        String text = it
        def regex = ~/import .*\.\*;/
        def m = regex.matcher(text)
        if (m.find()) {
            throw new AssertionError("Wildcard imports disallowed - ${m.findAll()}")
        }
    }
    java {
        googleJavaFormat()
        removeUnusedImports()
        custom 'disallowWildcardImports', disallowWildcardImports
    }
}

def reportsDir = "${buildDir}/reports";
def coverageDir = "${reportsDir}/coverage";

jacoco {
    toolVersion = '0.8.5'
    reportsDir = file(coverageDir)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination = file(coverageDir)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.openlineage'
            artifactId = 'openlineage-spark'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'openlineage-spark'
                description = 'Java library for OpenLineage'
                url = 'https://github.com/OpenLineage/OpenLineage'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'openlineage'
                        name = 'OpenLineage Project'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/OpenLineage/OpenLineage.git'
                    developerConnection = 'scm:git:ssh://github.com:OpenLineage/OpenLineage.git'
                    url = 'https://github.com/OpenLineage/OpenLineage'
                }
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "version": project.property("version")
        ]
    }

    repositories {
        maven {
            url = isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2' :
                    'https://datakin.jfrog.io/artifactory/maven-public-libs-snapshot'
            credentials {
                username = System.getenv('RELEASE_USERNAME')
                password = System.getenv('RELEASE_PASSWORD')
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

shadowJar {
    classifier = ''
    // avoid conflict with any client version of that lib
    relocate 'com.github.ok2c.hc5', 'openlineage.com.github.ok2c.hc5'
    relocate 'org.apache.httpcomponents.client5', 'openlineage.org.apache.httpcomponents.client5'
    relocate 'javassist', 'openlineage.javassist'

    manifest {
        attributes(
                "Premain-Class": 'io.openlineage.spark.agent.SparkAgent',
                "Agent-Class": 'io.openlineage.spark.agent.SparkAgent',
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version'),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    zip64 true
}

assemble {
    dependsOn shadowJar
}

task createVersionProperties(dependsOn: processResources) {
    doLast {
        File dir = new File("$buildDir/resources/main/io/openlineage/spark/agent/client/")
        dir.mkdirs();
        new File("$buildDir/resources/main/io/openlineage/spark/agent/client/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionProperties
}