/*
/* Copyright 2018-2025 contributors to the OpenLineage project
/* SPDX-License-Identifier: Apache-2.0
*/

plugins {
    id("java-library")
    id("pmd")
    id("com.diffplug.spotless")
    id("io.freefair.lombok")
    id("io.openlineage.common-config")
    id("io.openlineage.scala-variants")
    id("idea")
    id("java-test-fixtures")
}

scalaVariants {
    create("2.12")
    create("2.13")
}

idea {
    module {
        testSources.from(sourceSets.testScala212.java.srcDirs, sourceSets.testScala213.java.srcDirs)
    }
}

ext {
    assertjVersion = "3.27.3"
    icebergVersion = "0.14.1"
    jacksonVersion = "2.15.3"
    junit5Version = "5.13.0"
    mockitoVersion = "4.11.0"
    micrometerVersion = "1.15.0"

    sparkVersion = project.findProperty("spark33.spark.version")
    scalaBinaryVersion = project.findProperty("scala.binary.version")
    configurationName = scalaBinaryVersion.replace(".", "")
}

configurations.configureEach { // https://github.com/apache/spark/pull/38355 - can be remove for Spark 3.3.2
    resolutionStrategy {
        // https://github.com/FasterXML/jackson-databind/issues/3627
        force "com.fasterxml.jackson:jackson-bom:$jacksonVersion"
    }
}

dependencies {
    implementation(project(path: ":shared", configuration: "scala${configurationName}RuntimeElements"))
    implementation(project(path: ":spark3", configuration: "scala${configurationName}RuntimeElements"))

    compileOnly("org.apache.iceberg:iceberg-spark-runtime-3.3_${scalaBinaryVersion}:${icebergVersion}")
    compileOnly("org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkVersion}")
    compileOnly("io.micrometer:micrometer-core:${micrometerVersion}")

    // TODO: Replace these with 'testImplementation'
    testFixturesApi(project(path: ":shared", configuration: "scala${configurationName}RuntimeElements"))
    testFixturesApi("org.apache.iceberg:iceberg-spark-runtime-3.3_${scalaBinaryVersion}:${icebergVersion}")
    testFixturesApi("org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkVersion}")
    testFixturesApi("org.assertj:assertj-core:${assertjVersion}")
    testFixturesApi("org.junit.jupiter:junit-jupiter-api:${junit5Version}")
    testFixturesApi("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testFixturesApi("org.mockito:mockito-core:${mockitoVersion}")
    testFixturesApi("org.mockito:mockito-inline:${mockitoVersion}")
    testFixturesApi("io.micrometer:micrometer-core:${micrometerVersion}")

    // Scala 2.12
    scala212Implementation(project(path: ":shared", configuration: "scala212RuntimeElements"))
    scala212Implementation(project(path: ":spark3", configuration: "scala212RuntimeElements"))

    scala212CompileOnly("org.apache.iceberg:iceberg-spark-runtime-3.3_2.12:${icebergVersion}")
    scala212CompileOnly("org.apache.spark:spark-sql_2.12:${sparkVersion}")
    scala212CompileOnly("io.micrometer:micrometer-core:${micrometerVersion}")

    testScala212Implementation("org.apache.iceberg:iceberg-spark-runtime-3.3_2.12:${icebergVersion}")
    testScala212Implementation("io.micrometer:micrometer-core:${micrometerVersion}")
    testScala212Implementation("org.apache.spark:spark-sql_2.12:${sparkVersion}")
    testScala212Implementation("org.assertj:assertj-core:${assertjVersion}")
    testScala212Implementation("org.junit.jupiter:junit-jupiter-api:${junit5Version}")
    testScala212Implementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testScala212Implementation("org.mockito:mockito-core:${mockitoVersion}")
    testScala212Implementation("org.mockito:mockito-inline:${mockitoVersion}")

    // Scala 2.13
    scala213Implementation(project(path: ":shared", configuration: "scala213RuntimeElements"))
    scala213Implementation(project(path: ":spark3", configuration: "scala213RuntimeElements"))

    scala213CompileOnly("org.apache.iceberg:iceberg-spark-runtime-3.3_2.13:${icebergVersion}")
    scala213CompileOnly("org.apache.spark:spark-sql_2.13:${sparkVersion}")
    scala213CompileOnly("io.micrometer:micrometer-core:${micrometerVersion}")

    testScala213Implementation("org.apache.iceberg:iceberg-spark-runtime-3.3_2.13:${icebergVersion}")
    testScala213Implementation("org.apache.spark:spark-sql_2.13:${sparkVersion}")
    testScala213Implementation("io.micrometer:micrometer-core:${micrometerVersion}")
    testScala213Implementation("org.assertj:assertj-core:${assertjVersion}")
    testScala213Implementation("org.junit.jupiter:junit-jupiter-api:${junit5Version}")
    testScala213Implementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testScala213Implementation("org.mockito:mockito-core:${mockitoVersion}")
    testScala213Implementation("org.mockito:mockito-inline:${mockitoVersion}")
}
