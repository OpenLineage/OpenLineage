import org.apache.tools.ant.filters.ReplaceTokens
import groovy.io.FileType

import java.nio.file.Files


plugins {
    id 'java'
    id 'java-library'
    id 'java-test-fixtures'
    id 'com.diffplug.spotless' version '5.12.1'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "pmd"
}

pmd {
    consoleOutput = true
    toolVersion = "6.46.0"
    rulesMinimumPriority = 5
    ruleSetFiles = rootProject.files("pmd-openlineage.xml")
    ruleSets = []
    ignoreFailures = true
}

pmdMain {
    reports {
        html.required = true
    }
}

archivesBaseName='openlineage-spark-shared'


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://astronomer.jfrog.io/artifactory/maven-public-libs-snapshot'
    }
}

configurations {
    lombok
}

ext {
    assertjVersion = '3.23.1'
    bigqueryVersion = '0.26.0'
    junit5Version = '5.9.1'
    sparkVersion = '2.4.8'
    postgresqlVersion = '42.5.0'
    mockitoVersion = '4.9.0'
    testcontainersVersion = '1.17.6'
}

dependencies {
    api "io.openlineage:openlineage-java:${project.version}"

    compileOnly "org.apache.spark:spark-core_2.11:${sparkVersion}"
    compileOnly "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    compileOnly ("com.google.cloud.spark:spark-bigquery_2.11:${bigqueryVersion}") {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.module'
    }
    compileOnly ("net.snowflake:spark-snowflake_2.12:2.11.0-spark_3.3") {
        exclude group: 'com.google.guava'
    }

    compileOnly "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    compileOnly "org.apache.spark:spark-sql-kafka-0-10_2.11:${sparkVersion}"
    compileOnly "com.databricks:dbutils-api_2.11:0.0.5"

    testFixturesApi platform('org.junit:junit-bom:5.7.1')
    testFixturesApi "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testFixturesApi "org.junit.jupiter:junit-jupiter-params:${junit5Version}"

    testFixturesApi "org.postgresql:postgresql:${postgresqlVersion}"
    testFixturesApi 'org.hamcrest:hamcrest-library:2.2'
    testFixturesApi 'org.xerial:sqlite-jdbc:3.39.4.0'
    testFixturesApi "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testFixturesApi "org.testcontainers:postgresql:${testcontainersVersion}"
    testFixturesApi "org.testcontainers:mockserver:${testcontainersVersion}"
    testFixturesApi "org.testcontainers:kafka:${testcontainersVersion}"
    testFixturesApi "org.apache.kafka:kafka-clients:3.3.1"
    testFixturesApi('org.mock-server:mockserver-client-java:5.12.0') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
        exclude group: 'com.fasterxml.jackson.dataformat'
    }
    testFixturesApi group: 'org.awaitility', name: 'awaitility', version: '4.2.0'
    testFixturesApi "org.assertj:assertj-core:${assertjVersion}"
    testFixturesApi "org.mockito:mockito-core:${mockitoVersion}"
    testFixturesApi "org.mockito:mockito-inline:${mockitoVersion}"
    testFixturesApi "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    testImplementation "org.apache.spark:spark-core_2.11:${sparkVersion}"
    testImplementation "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    testImplementation "org.apache.spark:spark-hive_2.11:${sparkVersion}"
    testImplementation "com.google.cloud.spark:spark-bigquery-with-dependencies_2.11:${bigqueryVersion}"
}

def commonTestConfiguration = {
    forkEvery 1
    maxParallelForks 5
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    systemProperties = [
            'junit.platform.output.capture.stdout': 'true',
            'junit.platform.output.capture.stderr': 'true',
            'spark.version'                       : "${sparkVersion}",
            'openlineage.spark.jar'               : "${archivesBaseName}-${project.version}.jar",
            'kafka.package.version'               : "org.apache.spark:spark-sql-kafka-0-10_2.11:${sparkVersion}",
            'mockserver.logLevel'                 : 'ERROR'
    ]

    classpath = project.sourceSets.test.runtimeClasspath
}


test {
    configure commonTestConfiguration
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
}

assemble {
    dependsOn shadowJar
}

shadowJar {
    minimize()
    classifier = ''
    zip64 true
}

spotless {
    def disallowWildcardImports = {
        String text = it
        def regex = ~/import .*\.\*;/
        def m = regex.matcher(text)
        if (m.find()) {
            throw new AssertionError("Wildcard imports disallowed - ${m.findAll()}")
        }
    }
    java {
        googleJavaFormat()
        removeUnusedImports()
        custom 'disallowWildcardImports', disallowWildcardImports
    }
}