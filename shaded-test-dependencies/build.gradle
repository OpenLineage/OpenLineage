plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'com.google.cloud.hive'

ext {
    hiveVersion = '3.1.3'
    hadoopVersion = '2.10.2'
    tezVersion = '0.9.2'
    pigVersion = '0.17.0'
    pigClassifier = ''
}

dependencies {
    // Hadoop dependencies
    implementation("org.apache.hadoop:hadoop-common:${hadoopVersion}") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.avro', module: 'avro'
    }
    implementation("org.apache.hadoop:hadoop-mapreduce-client-common:${hadoopVersion}") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.avro', module: 'avro'
    }

    // Hive dependencies
    implementation("org.apache.hive:hive-exec:${hiveVersion}") {
        exclude group: 'org.pentaho', module: '*'
        exclude group: 'org.apache.hadoop', module: '*'
    }
    implementation("org.apache.hive:hive-common:${hiveVersion}") {
        exclude group: 'org.apache.hadoop', module: '*'
        exclude group: 'com.github.joshelser', module: 'dropwizard-metrics-hadoop-metrics2-reporter'
    }
    implementation("org.apache.hive:hive-service:${hiveVersion}") {
        exclude group: 'org.pentaho', module: '*'
        exclude group: 'org.apache.hadoop', module: '*'
        exclude group: 'org.apache.hbase', module: '*'
        exclude group: 'org.apache.parquet', module: 'parquet-hadoop-bundle'
        exclude group: 'com.github.joshelser', module: 'dropwizard-metrics-hadoop-metrics2-reporter'
        exclude group: 'com.lmax', module: 'disruptor'
    }
    implementation("org.apache.hive.hcatalog:hive-hcatalog-pig-adapter:${hiveVersion}") {
        exclude group: 'org.apache.hive', module: 'hive-exec'
        exclude group: 'org.apache.hadoop', module: '*'
        exclude group: 'org.apache.pig', module: '*'
    }

    // Tez dependencies
    implementation("org.apache.tez:tez-dag:${tezVersion}") {
        exclude group: 'org.apache.hadoop', module: '*'
    }
    implementation("org.apache.tez:tez-common:${tezVersion}") {
        exclude group: 'org.apache.hadoop', module: '*'
    }
    implementation("org.apache.tez:tez-mapreduce:${tezVersion}") {
        exclude group: 'org.apache.hadoop', module: '*'
    }

    // Pig dependency
    implementation("org.apache.pig:pig:${pigVersion}${pigClassifier ? ":${pigClassifier}" : ''}")
}

configurations {
    compileOnly {
        extendsFrom implementation
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

shadowJar {
    archiveClassifier.set('')

    zip64 true

    exclude 'module-info.class'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'

    // Exclude Datanucleus packages:
    exclude 'org/datanucleus/**'

    relocate 'com.google.common', 'hive.shaded.com.google.common'
    relocate 'com.google.gson', 'hive.shaded.com.google.gson'
    relocate 'com.google.protobuf', 'hive.shaded.com.google.protobuf'
    relocate 'io.netty', 'hive.shaded.io.netty'
    relocate 'com.lmax', 'disruptor'

    mergeServiceFiles()
}