plugins {
    id 'java'
    // Don't bump above 6.13 - it requires Java 11 https://github.com/diffplug/spotless/blob/main/plugin-gradle/CHANGES.md#changes-12
    id 'com.diffplug.spotless' version '6.13.0'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'com.gradleup.shadow' version '8.3.3'
    id 'pmd'
}

group = 'io.openlineage'
version = '1.0.0-SNAPSHOT'

subprojects {
    tasks.withType(Test).tap {
        configureEach {
            // Prevent Gradle from caching tests results
            outputs.upToDateWhen { false }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'pmd'
    repositories {
        mavenCentral()
        mavenLocal()

        // FIXME: We use this repo to only download the JAR because hive-bigquery-connector:2.0.3
        //  has some incorrect metadata in its POMs. This will be fixed in a future release of the
        //  connector.
        mavenCentral() {
            metadataSources { artifact() }
        }
    }
    pmd {
        consoleOutput = true
        toolVersion = "6.46.0"
        rulesMinimumPriority = 5
        ruleSetFiles = rootProject.files("pmd-config.xml")
        ruleSets = []
        ignoreFailures = false
    }
    pmdMain {
        reports {
            html.required = true
        }
    }
    spotless {
        java {
            target("src/**/*.java")
            googleJavaFormat()
            removeUnusedImports()
            // FIXME: Which copyright? Google Inc.?
            licenseHeader("""/*
 * Copyright \$YEAR Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */""")
        }
    }
}
