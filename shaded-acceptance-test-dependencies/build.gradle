import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.google.cloud.hive'

ext {
    dataprocVersion = '4.45.0'
}

dependencies {
    implementation("com.google.cloud:google-cloud-dataproc:${dataprocVersion}")
}

configurations {
    compileOnly {
        extendsFrom implementation
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

shadowJar {
    // Don't need a classifier since the module is already has "shaded" in its name
    archiveClassifier.set('')
    zip64 true

    exclude 'module-info.class'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'

    // Exclude all Datanucleus packages
    exclude 'org/datanucleus/**'

    // Relocate some packages
    [
            'com.google.cloud.dataproc',
    ].each { packageName ->
        relocate packageName, "hive.acceptance.shaded.${packageName}"
    }

    mergeServiceFiles()

    // Avoid some pesky Log4j warnings
    transform(Log4j2PluginsCacheFileTransformer)
}