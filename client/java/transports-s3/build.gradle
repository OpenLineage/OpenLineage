/*
/* Copyright 2018-2025 contributors to the OpenLineage project
/* SPDX-License-Identifier: Apache-2.0
*/

plugins {
    id 'eclipse'
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'com.diffplug.spotless' version '7.0.2'
    id "com.gradleup.shadow" version "9.2.2"
    id "pmd"
    id "io.freefair.lombok" version "9.1.0"
    id "com.github.gmazzo.buildconfig" version "6.0.6"
}

ext {
    projectDescription = "S3 OpenLineage transport library"
    s3MockVersion = "4.10.0"
    testcontainersVersion = "2.0.2"
    awssdkVersion = "2.40.0"
}

sourceSets {
    test {
        buildConfig {
            // We should use the same version of dependency and Docker image
            buildConfigField(String, "S3_MOCK_VERSION", s3MockVersion)
        }
    }
}

dependencies {
    compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    implementation(platform("software.amazon.awssdk:bom:${awssdkVersion}"))
    implementation("software.amazon.awssdk:auth:${awssdkVersion}")
    implementation("software.amazon.awssdk:s3:${awssdkVersion}")
    implementation("software.amazon.awssdk:url-connection-client:${awssdkVersion}")

    testImplementation("com.adobe.testing:s3mock-testcontainers:${s3MockVersion}")
    testImplementation(platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}"))
    testImplementation("org.testcontainers:testcontainers-junit-jupiter:${testcontainersVersion}")
}

shadowJar {
    relocate "software.amazon", "io.openlineage.client.shaded.software.amazon"
    relocate "org.apache", "io.openlineage.client.shaded.org.apache"
    relocate "org.reactivestreams", "io.openlineage.client.shaded.org.reactivestreams"
    relocate "org.slf4j", "io.openlineage.client.shaded.org.slf4j"
}

apply from: '../transports.build.gradle'
