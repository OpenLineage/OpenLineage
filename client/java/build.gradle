/*
/* Copyright 2018-2025 contributors to the OpenLineage project
/* SPDX-License-Identifier: Apache-2.0
*/

import org.apache.tools.ant.filters.*

plugins {
    id 'eclipse'
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'com.diffplug.spotless' version '7.0.2'
    id "org.openapi.generator" version "6.6.0"
    id "com.gradleup.shadow" version "8.3.6"
    id "pmd"
    id "io.freefair.lombok" version "8.13.1"
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = "io.openlineage"


pmd {
    consoleOutput = true
    toolVersion = "6.46.0"
    rulesMinimumPriority = 5
    ruleSetFiles = rootProject.files("pmd-openlineage.xml")
    ruleSets = []
    ignoreFailures = false
}

pmdMain {
    reports {
        html.required = true
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    assertjVersion = '3.27.3'
    jacksonVersion = "2.15.3"
    junit5Version = '5.11.4'
    lombokVersion = '1.18.38'
    mockitoVersion = '5.17.0'
    micrometerVersion = '1.14.5'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
    guavaVersion = '33.4.7-jre'
}

configurations {
    codeGenerator
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
    implementation 'commons-logging:commons-logging:1.3.5'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation "io.micrometer:micrometer-core:${micrometerVersion}"

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.apache.kafka:kafka-clients:4.0.0'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "io.micrometer:micrometer-registry-statsd:${micrometerVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    codeGenerator project(':generator')

    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}") {
        exclude(group: 'org.junit.jupiter')
    }
    testImplementation "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "io.micrometer:micrometer-registry-statsd:${micrometerVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "com.google.guava:guava:${guavaVersion}"
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation("org.mock-server:mockserver-netty:5.14.0:shaded") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.datatype'
        exclude group: 'com.fasterxml.jackson.dataformat'
        exclude group: 'org.mock-server.mockserver-client-java'
    }
}

configurations {
    testImplementation.extendsFrom compileOnly
}

compileJava {
    options.incremental = true
    options.compilerArgs << '-parameters'
    options.encoding = "UTF-8"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    options.incremental = true
    options.compilerArgs << '-parameters'
    options.encoding = "UTF-8"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task generateCode(type: JavaExec) {
    description 'generate java code from spec.'
    classpath configurations.codeGenerator
    mainClass = 'io.openlineage.client.Generator'
    def rootUrl = "file:$rootDir/../../spec/".toString()
    args rootUrl + "OpenLineage.json"
    args rootUrl
    outputs.files("src/main/java/server", "src/main/java/client/OpenLineage.java")
}

openApiGenerate {
// openapi-generator generate -i spec/OpenLineage.yml -g html2 -o ../OpenLineage.github.io/
    generatorName = "html2"
    inputSpec = "$rootDir/../../spec/OpenLineage.yml".toString()
    outputDir = "$buildDir/docs".toString()
}

task sourceJar(type: Jar) {
    archiveClassifier='sources'
    from sourceSets.main.allJava
}

javadoc {
    dependsOn delombok
    failOnError = true

    options.addBooleanOption("Xdoclint:-missing", true)

    def capturedOutput = []
    def listener = { capturedOutput << it } as StandardOutputListener
    doFirst {
        logging.addStandardErrorListener(listener)
        logging.addStandardOutputListener(listener)
    }
    doLast {
        logging.removeStandardOutputListener(listener)
        logging.removeStandardErrorListener(listener)
        capturedOutput.each { e ->
            if(e.toString() =~ " warning: ") {
                throw new GradleException("You have some javadoc warnings, please fix them!");
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier='javadoc'
    from javadoc.destinationDir
}

delombok.dependsOn tasks.generateCode
sourceJar.dependsOn tasks.generateCode
compileJava.dependsOn tasks.generateCode
sourceSets.main.java.srcDirs tasks.generateCode

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.openlineage'
            artifactId = 'openlineage-java'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'openlineage-java'
                description = 'Java library for OpenLineage'
                url = 'https://github.com/OpenLineage/OpenLineage'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'openlineage'
                        name = 'OpenLineage Project'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/OpenLineage/OpenLineage.git'
                    developerConnection = 'scm:git:ssh://github.com:OpenLineage/OpenLineage.git'
                    url = 'https://github.com/OpenLineage/OpenLineage'
                }
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "version": project.property("version")
        ]
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('RELEASE_USERNAME')
            password = System.getenv('RELEASE_PASSWORD')
        }
    }
}

signing {
    required { isReleaseVersion }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

jar {
    manifest {
        attributes(
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version'),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude 'src/main/java/io/openlineage/server/**/*.java',
                    'src/main/java/io/openlineage/client/OpenLineage.java',
                    '**/build/**',
                    '**/generator/**'
        }
        googleJavaFormat()
        removeUnusedImports()
    }
}

spotlessJava.dependsOn generateCode

def reportsDir = "${buildDir}/reports";
def coverageDir = "${reportsDir}/coverage";

jacoco {
    toolVersion = '0.8.12'
    reportsDir = file(coverageDir)
}

jacocoTestReport {
    reports {
        xml {
            enabled true
        }
        html {
            enabled true
            destination = file(coverageDir)
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "io/openlineage/client/OpenLineage.class",
                    "io/openlineage/client/OpenLineage\$*.class"
            ])
        }))
    }
}
